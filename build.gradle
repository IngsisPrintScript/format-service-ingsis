plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'com.diffplug.spotless' version '6.25.0'
	id 'checkstyle'
	id 'jacoco'
}

group = 'com.ingsis'
version = '0.0.1-SNAPSHOT'
description = 'Format Service for document processing'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(22)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

tasks.named('test') {
	useJUnitPlatform()
	finalizedBy(tasks.named('jacocoTestReport'))
}

jacoco {
	toolVersion = '0.8.11'
}

tasks.named('jacocoTestReport', JacocoReport) {
	dependsOn(tasks.test)
	reports {
		xml.required = true
		html.required = true
		csv.required = false
	}
}

tasks.named('jacocoTestCoverageVerification', JacocoCoverageVerification) {
	dependsOn(tasks.test)
	violationRules {
		rule {
			limit { minimum = 0.30 } //cambiar a 0.80
		}
	}
}

checkstyle {
	toolVersion = '10.15.0'
	config = resources.text.fromFile('config/checkstyle/checkstyle.xml')
}

spotless {
	java {
		googleJavaFormat()
		target 'src/**/*.java'
	}
	format 'gradle', {
		target '*.gradle'
		trimTrailingWhitespace()
		endWithNewline()
	}
}

tasks.named('check') {
	dependsOn 'spotlessCheck', 'checkstyleMain', 'jacocoTestReport', 'jacocoTestCoverageVerification'
}
